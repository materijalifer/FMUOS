-- Copyright 2020 Studosi
--
-- Licensed under the Apache License, Version 2.0 (the "License");
-- you may not use this file except in compliance with the License.
-- You may obtain a copy of the License at
--
--     http://www.apache.org/licenses/LICENSE-2.0
--
-- Unless required by applicable law or agreed to in writing, software
-- distributed under the License is distributed on an "AS IS" BASIS,
-- WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
-- See the License for the specific language governing permissions and
-- limitations under the License.

-- Players: ferryman, wolf, goat, cabbage.
-- Problem:
-- 	All start on one (right) side of the river.
-- 	All should end up on the other (left) side of the river.
--	The ferryman can only take one object at a time.
--	If left alone without the ferryman:
--		The wolf eats the goat, or
-- 		The goat eats the cabbage.

MODULE main
VAR
-- the ferryman and the three items
  cabbage  : {right,left};
  goat     : {right,left};
  wolf     : {right,left};
  ferryman : {right,left};
  carry_cabbage: boolean;
  carry_goat : boolean;
  carry_wolf : boolean;
  no_carry : boolean;
  
-- possible moves
  move    : {c, g, w, e};


-- initially everything is on the right bank
ASSIGN
  init(cabbage)  := right;
  init(goat)     := right;
  init(wolf)     := right;
  init(ferryman) := right;

  carry_cabbage := move=c;
  carry_goat := move=g;
  carry_wolf := move=w;
  no_carry := move=e;
  
TRANS
  carry_cabbage -> 
		cabbage=ferryman & -- cabbage is carried by the ferryman
		next(cabbage)!=cabbage &
		next(ferryman)!=ferryman &
		next(goat)=goat &
		next(wolf)=wolf

TRANS
  carry_goat -> 
		goat=ferryman & 
		next(goat)!=goat & 
		next(ferryman)!=ferryman &
		next(cabbage)=cabbage &
		next(wolf)=wolf


TRANS
  carry_wolf -> 
		wolf=ferryman & 
		next(wolf)!=wolf & 
		next(ferryman)!=ferryman &
		next(goat)=goat &
		next(cabbage)=cabbage

TRANS
  no_carry -> 
	next(ferryman)!=ferryman &
	next(goat)=goat &
	next(cabbage)=cabbage &
	next(wolf)=wolf 
		

-- goat and wolf must not be left unattended !
-- goat and cabbage must not be left unattended !
DEFINE
  safe_state := (goat = wolf | goat = cabbage) -> goat = ferryman;
  goal := cabbage = left & goat = left & wolf = left;


CTLSPEC A[!goal U !safe_state]